

## 第八章 字典：图，哈希，关键字列表，集合，结构

本章内容：

* 2个半字典类型
* 通用字典API
* 模式匹配和图的更新
* 结构
* 复合嵌套结构


字典就是使用键（key）和值（value）组成的数据结构。

我们已经见过了一些字典类型了：图和关键字列表。本章将会介绍他们的模式匹配和操作更新。还会学习一种新的字典类型哈希（HashDict）。最后将会讨论Keyword模块，该模块提供了操作字典的必要方法和函数，还有Set模块，实现了关于集合的数据结构。

首先，我们面临一个很普遍的问题，我们到底需要什么样的类字典结构呢？


### 图，哈希，关键字列表三种如何选取？

可以自问下面问题：

* 字典是否需要相同的key？

  如果是，那么可以使用关键字列表。

* 字典元素是否是有序的？
  
  如果是，那么还是使用关键字列表。

* 是否需要根据内容进行模式匹配（例如，匹配一个字典带有一个键为 :name的元素）?

  如果是，那么可以使用图。
  	
* 是否需要存储上百个元素？
 
  如果是，那么最好使用哈希。`Erlang`虚拟机的`R17`版本中，图比较慢，尤其是增加新元素的时候。`R18`将会优化。

### 字典们

图和哈希都实现了字典的行为。关键字列表则实现了主要的行为。他们的主要差别在于关键字列表允许有多个键。


通常，你可以使用`Dict`模块的功能，它可以灵活的操作图和哈希结构。（下面的例子中使用了Enum.into，该函数可以让你轻松的将一个图或者哈希添加到关键字列表之前，并返回连接的图或者哈希。在集合协议（Collectable Protocol）中将会有更多的介绍）

maps/use_dict.exs

```elixir
defmodule Sum do

	def values(dict) do
		dict |> Dict.values |> Enum.sum
	end
end
```

### 哈希求和

```elixir
hd = [ one: 1, two: 2, three: 3 ] |> Enum.into HashDict.new

IO.puts Sum.values(hd) 		# => 6
```


### 图求和


```elixir
map = %{ four: 4, five: 5, six: 6 }

IO.puts Sum.values(map) # => 15
```

尝试`Dict`模块`API`

```elixir
iex> kw_list = [name: "Dave", likes: "Programming", where: "Dallas"]
[name: "Dave", likes: "Programming", where: "Dallas"]
iex> hashdict = Enum.into kw_list, HashDict.new
#HashDict<[name: "Dave", where: "Dallas", likes: "Programming"]>
iex> map = Enum.into kw_list, MaDhp.new
%{likes: "Programming", name: "Dave", where: "Dallas"}
iex> kw_list[:name]
"Dave"
iex> hashdict[:likes]
"Programming"
iex> map[:where]
"Dallas"
iex> hashdict = Dict.drop(hashdict, [:where, :likes])
#HashDict<[name: "Dave"]>
iex> hashdict = Dict.put(hashdict, :also_likes, "Ruby")
#HashDict<[name: "Dave", also_likes: "Ruby"]>
iex> combo = Dict.merge(map, hashdict)
%{also_likes: "Ruby", likes: "Programming", name: "Dave", where: "Dallas"}
```

关键字列表允许有多个相同的键，但是键值：

```eleixr
iex> kw_list = [name: "Dave", likes: "Programming", likes: "Elixir"]
[name: "Dave", likes: "Programming", likes: "Elixir"]
iex> kw_list[:likes]
"Programming"
iex> Dict.get(kw_list, :likes)
"Programming"
iex> Keyword.get_values(kw_list, :likes)
["Programming", "Elixir"]
```

你可以访问完整的[API文档](http://elixir-lang.org/docs/)，花一点实际自己熟悉它们的操作。

### 模式匹配和图更新操作

对于图，最常见的问题就是：

* 是否有这些键或者这些值呢？

* 是否个有一项的键是`:name`？

* 是否有一些项的键是 `:name` 和 `:height`？

* `:name`键的值是否是 "Dave"？


下面是使用Elixir模式匹配来回答上述问题：

```elixir
iex> person = %{ name: "Dave", height: 1.88 }
%{height: 1.88, name: "Dave"}
iex> %{ name: a_name } = person
%{height: 1.88, name: "Dave"}
iex> a_name
"Dave"
iex> %{ name: _, height: _ } = person
%{height: 1.88, name: "Dave"}
iex> %{ name: "Dave" } = person
%{height: 1.88, name: "Dave"}
```

图`person`没有`:weight`键，所以下面的模式匹配不成功：

```elixir
iex> %{ name: _, weight: _ } = person
** (MatchError) no match of right hand side value: %{height: 1.88, name: "Dave"}
```


非常值得学习第一个图的是如何取出键`:name`进行模式匹配。可以通过多种方式使用。下面有一个例子，在列表解析（list comprehension）中使用图的结构来快速查找。

maps/query.exs

```elixir
people = [
	%{ name: "Grumpy", height: 1.24 },
	%{ name: "Dave", height: 1.88 },
	%{ name: "Dopey", height: 1.32 },
	%{ name: "Shaquille", height: 2.16 },
	%{ name: "Sneezy", height: 1.28 }
]

for person = %{ height: height } <- people, height > 1.5, do: IO.inspect person


for %{ height: height } = person <- people, height > 1.5, do: IO.inspect person

```

产生的结果：

```elixir
%{height: 1.88, name: "Dave"}
%{height: 2.16, name: "Shaquille"}
```

上面的代码，对`people`图进行了列表解析。生成子句绑定了每一个图的元素，同时对`:heigth`进行模式匹配。并且还使用`height > 1.5`作为每一个模式匹配的过滤条件，最终打印出匹配结果图。

模式匹配就是模式匹配，图可以在在cond表达式中，函数子句，还是其他可以使用模式匹配的情况。



maps/book_room.exs

```elixir
defmodule HotelRoom do

	def book(%{name: name, height: height}) when height > 1.9 do

		IO.puts "Need extra long bed for #{name}"
	end

	def book(%{name: name, height: height}) when height < 1.3 do

		IO.puts "Need low shower controls for #{name}"
	end

	def book(person) do

		IO.puts "Need regular bed for #{person.name}"
	end
end

people |> Enum.each(&HotelRoom.book/1)

# Need low shower controls for Grumpy
# Need regular bed for Dave
# Need regular bed for Dopey
# Need extra long bed for Shaquille
# Need low shower controls for Sneezy
```


### 模式匹配无法绑定键

模式匹配中，并不能给图的键绑定值。也就是说键必须是键，不能是变量。但是可以给值绑定变量：

```elixir
iex> %{ 2 => state } = %{ 1 => :ok, 2 => :error }
%{1 => :ok, 2 => :error}
iex> state
:error
```

键不能绑定变量的值，不能这么写：

```elixir
iex> %{ item => :ok } = %{ 1 => :ok, 2 => :error }
** (CompileError) iex:5: illegal use of variable item in map key
```

### 更新图

前一章介绍了列表的更新操作。

对于图，可以不需要遍历整个图就能增加新的键值对项。但是Elixir中的所有数据结构，包括图也是不可变的，因此所谓的更新，其实是返回了一个新的图。


最简单的更新图语法：


```elixir
new_map = %{ old_map | key => value, … }
```


This creates a new map that is a copy of the old, but the values associated with the keys on the right of the pipe character are updated:

```elixir
iex> m = %{ a: 1, b: 2, c: 3 }
%{a: 1, b: 2, c: 3}
iex> m1 = %{ m | b: "two", c: "three" }
%{a: 1, b: "two", c: "three"}
iex> m2 = %{ m1 | a: "one" }
%{a: "one", b: "two", c: "three"}
```

该语法不能给图增加一个新的项。可以使用` Dict.put_new/3`函数增加新的键值。

### 图和结构

当Elixir看见`%{ … }`这样的结构，则识别为图。但也仅限于此，它不能知道更多的信息了。有时候，你并不清楚自己使用图的目的，无论是否需要键，还是这些键是否有对应的默认值。

就像是匿名图。如果你想创建一种类似图的结构。它有一些字段像键一样，可以有值，也可以没有值，并且还能进行模式匹配。


### 走进结构（struct）

结构图是基于图的一个扩展模块。相对于图，结构图还有一些限制。例如结构图的键必须是原子，同时它并没有实现字典的行为。

定义的模块名就是改结构图的类型名。在模块内，你可以使用`defstruct`宏来定义结构图（独特的图）。

maps/defstruct.exs

```elxiir
defmodule Subscriber do
	defstruct name: "", paid: false, over_18: true
end
```

打开iex实战：

```elixir
$ iex defstruct.exs

iex> s1 = %Subscriber{}
%Subscriber{name: "", over_18: true, paid: false}
iex> s2 = %Subscriber{ name: "Dave" }
%Subscriber{name: "Dave", over_18: true, paid: false}
iex> s3 = %Subscriber{ name: "Mary", paid: true }
%Subscriber{name: "Mary", over_18: true, paid: true}
```

创建结构图的语法和创建图的语法一样，只需要在花括号和百分号之间写入结构图的类型即可

可是通过`.`来访问结构图的键值， 结构图还可以进行模式匹配：

```elixir
iex> s3.name
"Mary"
iex> %Subscriber{name: a_name} = s3
%Subscriber{name: "Mary", over_18: true, paid: true}
iex> a_name
"Mary"
```

更新也很方便：

```elixir
iex> s4 = %Subscriber{ s3 | name: "Marie"}
%Subscriber{name: "Marie", over_18: true, paid: true}
```
为什么结构图要被包裹在模块里面呢？答案是你可能会增加额外的结构图功能。


maps/defstruct1.exs

```elixir
defmodule Attendee do

	defstruct name: "", paid: false, over_18: true

	def may_attend_after_party(attendee = %Attendee{}) do

		attendee.paid && attendee.over_18
	end

	def print_vip_badge(%Attendee{name: name}) when name != "" do

		IO.puts "Very cheap badge for #{name}"
	end

	def print_vip_badge(%Attendee{}) do

		raise "missing name for badge"
	end
end
```

编译

```elixir
$ iex defstruct1.exs
iex> a1 = %Attendee{name: "Dave", over_18: true}
%Attendee{name: "Dave", over_18: true, paid: false}
iex> Attendee.may_attend_after_party(a1)
false
iex> a2 = %Attendee{a1 | paid: true}
%Attendee{name: "Dave", over_18: true, paid: true}
iex> Attendee.may_attend_after_party(a2)
true
iex> Attendee.print_vip_badge(a2)
Very cheap badge for Dave
:ok
iex> a3 = %Attendee{}
%Attendee{name: "", over_18: true, paid: false}
iex> Attendee.print_vip_badge(a3)
** (RuntimeError) missing name for badge
defstruct1.exs:13: Attendee.print_vip_badge/1
```

注意上面的代码演示了如何在`Attendee`模块中通过函数操作结构图。

当实现多态的时候结构图还有一堆规则，这涉及到协议（`protocols`）相关的知识。

### 访问结构图的新方式

前面的例子展示了通过`.`访问结构图的属性。你可能会觉得惊讶，结构图和图普遍相似，你可可以使用键的访问图的值--`some_map[:name]`。

这里面的原理是因为图实现了访问协议（Access protocol），该协议定义了使用方括号和字段的方式访问结构的能力。虽然结构图没有实现这样的协议，大事你可以使用@derive指令来使用Access协议给结构图添加键访问的功能：

maps/derive.exs

```elixir
defmodule Attendee do
	@derive Access
	defstruct name: "", over_18: false
end
```

编译

```elixir
$ iex derive.exs

iex> a = %Attendee{name: "Sally", over_18: true}
%Attendee{name: "Sally", over_18: true}
iex> a[:name]
"Sally"
iex> a[:over_18]
true
iex> a.name
"Sally"
```

### 嵌套字典结构

不同的字典类型都是使用键和值构成的。但是值也可以是字典类型。下面实现一个功能，定义一个bug报告系统，包括两个模块，第一个是`BugReport `，用于展示bug情况；另外一个用户（customer）模块，用于查看bug。例如：

maps/nested.exs

```elixir
defmodule Customer do

	defstruct name: "", company: ""
end

defmodule BugReport do

	defstruct owner: %{}, details: "", severity: 1

end
```

先创建一个简单的报告

```elixir
iex> report = %BugReport{owner: %Customer{name: "Dave", company: "Pragmatic"},
...> details: "broken"}
%BugReport{details: "broken",
owner: %Customer{company: "Pragmatic", name: "Dave"},
severity: 1}
```

`report`本身是一个结构图，同时它的`owner`的属性是`Customer`结构图。可以通过`.`访问结构图的属性：

```elixir
iex> report.owner.company
"Pragmatic"
```

可是，现在的有用户投诉说，公司的名称不正确，即company的值不对，因此我们需要修改这个嵌套结构图：

```elixir
iex> report = %BugReport{ report | owner:
...> %Customer{ report.owner | company: "PragProg" }}
%BugReport{details: "broken",
owner: %Customer{company: "PragProg", name: "Dave"},
severity: 1}
```
上述的方式太丑陋了为了更新customer结构图，我们不得不把report的owner属性也写了。这太冗长了，不易读，还容易出错。

幸运的，`Elixir`提供了一些操作嵌套字典的函数。`put_in`则是其中一个，它可以在嵌套字典结构图中设置一个值：

```elixir
iex> put_in(report.owner.company, "PragProg")
%BugReport{details: "broken",
owner: %Customer{company: "PragProg", name: "Dave"},
severity: 1}
```
这不是魔法了。这是一个简单的宏，可以生成上面我们所写的那一长串代码。

`update_in`函数可以把结构图的值传入一个函数调用返回的结果作为新值。

```elixir
iex> update_in(report.owner.name, &("Mr. " <> &1))
%BugReport{details: "broken",
owner: %Customer{company: "PragProg", name: "Mr. Dave"},
severity: 1}
```

另外还有两个存取嵌套字典的函数`get_in` 和 `get_and_update_in`，具体用法可以使用iex查看帮助文档。这些函数最cool的就是具有存取嵌套访问能力。

嵌套存取（Accessors）and 无结构（Nonstructs）

嵌套存取函数通过了Access协议取出和重构了结构图类型。这意味着，如果使用图或者关键字列表的时候，也可以通过原子符号来使用这些函数。（这些函数存取的时候指定某个键的时候，结构图使用`.`，图和关键字列表使用`原子`）

```elixir
iex> report = %{ owner: %{ name: "Dave", company: "Pragmatic" }, severity: 1}
%{owner: %{company: "Pragmatic", name: "Dave"}, severity: 1}
iex> put_in(report[:owner][:company], "PragProg")
%{owner: %{company: "PragProg", name: "Dave"}, severity: 1}
iex> update_in(report[:owner][:name], &("Mr. " <> &1))
%{owner: %{company: "Pragmatic", name: "Mr. Dave"}, severity: 1}
```

### 动态(`Dynamic`)（运行时(`Runtime`)）嵌套存储


我们已经使用了宏来操作嵌套结构的存取，宏是在编译的时候展开的。因此，他们还是有一些限制的：

* 传递的键的个数是静止的。（不懂）
The number of keys you pass a particular call is static.
* 不能把这些键当成参数在函数中传递。
You can’t pass the set of keys as parameters between functions.
* 编译的时候，宏有更自然的把参数展开到代码中。

为了去除这些限制，这些宏（`get_in`, `put_in`, `update_in`, 和 `get_and_update_in`）都可以接受一些键作为序列参数传递。增加这些参数改变了宏函数的调用，因此变得动态。
 
| 方法                | 宏             | 函数                   |
|:-------------------|---------------:|:---------------------:|
| get_in             | no             | (dict, keys)          |
| put_in             | (path, value)  | (dict, keys, value)   |               
| update_in          | (path, fn)     | (dict, keys, fn)      |             
| get_and_update_in  | (path, fn)     | (dict, keys, fn)      |           


简单的例子：

maps/dynamic_nested.exs

```elxir
nested = %{
	buttercup: %{
		actor: %{
			first: "Robin",
			last: "Wright"
		},
		role: "princess"
	},
	westley: %{
		actor: %{
			first: "Carey",
			last: "Ewes" # typo!
		},
		role: "farm boy"
	}
}

IO.inspect get_in(nested, [:buttercup])
# => %{actor: %{first: "Robin", last: "Wright"}, role: "princess"}

IO.inspect get_in(nested, [:buttercup, :actor])
# => %{first: "Robin", last: "Wright"}

IO.inspect get_in(nested, [:buttercup, :actor, :first])
# => "Robin"

IO.inspect put_in(nested, [:westley, :actor, :last], "Elwes")
# => %{buttercup: %{actor: %{first: "Robin", last: "Wright"}, role: "princess"},
# => westley: %{actor: %{first: "Carey", last: "Elwes"}, role: "farm boy"}}
```

这段代码展示了`get_in`和`get_and_update_in`在动态方面的能力。可以把函数当成键的值传递，然后函数就会被调用并返回值作为键的新值。

maps/get_in_func.exs

```elixir
authors = [

	%{ name: "José", language: "Elixir" },
	%{ name: "Matz", language: "Ruby" },
	%{ name: "Larry", language: "Perl" }
]


languages_with_an_r = fn (:get, collection, next_fn) ->

	for row <- collection do

		if String.contains?(row.language, "r") do
			next_fn.(row)
		end
	end
end

IO.inspect get_in(authors, [languages_with_an_r, :name])
#=> [ "José", nil, "Larry" ]
```

### 集合（Sets）

目前为止只有一种集合的实现，即`HashSet`。

```elixir
iex> set1 = Enum.into 1..5, HashSet.new
#HashSet<[1, 2, 3, 4, 5]>
iex> Set.member? set1, 3
true
iex> set2 = Enum.into 3..8, HashSet.new
#HashSet<[3, 4, 5, 6, 7, 8]>
iex> Set.union set1, set2
#HashSet<[7, 6, 4, 1, 8, 2, 3, 5]>
iex> Set.difference set1, set2
#HashSet<[1, 2]>
iex> Set.difference set2, set1
#HashSet<[6, 7, 8]>
iex> Set.intersection set1, set2
#HashSet<[3, 4, 5]>
```

### 力量越大，诱惑越大

字典类型是一个十分强大并且常用的工具。但也容易被滥用。结构图可能会让你陷入黑暗中，因为它们可以在模块的函数内定义，以至于容易和函数搞混。这种情况下，如果你的大脑中传统的面对对象编程思想的神经元还没有被割裂的话，那么你可能会联想：“这有一点像类的定义哦”。是的，你是对的。使用模块，图或者结构图可以让写出类似面向对象的代码。

但是这是一个糟糕的想法。并不是因为对象本质上是糟糕的，而是因为混用了并且消弱了函数式编程带给来的好处。
保持纯净，年青的码农，保持纯净。（Stay pure, young coder. Stay pure.）

为了不至于是你陷入黑暗，下一章，我们将会有一个小的话题，讨论函数分离的优点和它们作用的数据。假装我们在讨论类型。





